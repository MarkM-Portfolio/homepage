-- ***************************************************************** 
--                                                                   
-- IBM Confidential                                                  
--                                                                   
-- OCO Source Materials                                              
--                                                                   
-- Copyright IBM Corp. 2001, 2015                                    
--                                                                   
-- The source code for this program is not published or otherwise    
-- divested of its trade secrets, irrespective of what has been      
-- deposited with the U.S. Copyright Office.                         
--                                                                   
-- ***************************************************************** 

-- 5724_S68                                              
USE HOMEPAGE;
GO

BEGIN TRANSACTION
GO

--------------------------------------
-- DROP THE INDEXES
--------------------------------------
DROP INDEX USERID_WIDGET_UNIQUE ON HOMEPAGE.USER_WIDGET_PREF;
GO

--------------------------------------
-- DROP THE CONSTRAINTS
--------------------------------------
ALTER TABLE HOMEPAGE.PREREQ DROP CONSTRAINT FK_PREREQ_WIDGET;
GO

ALTER TABLE HOMEPAGE.USER_WIDGET_PREF DROP CONSTRAINT FK_WIDGET_ID;
GO

-- adding PERSON_ID column
ALTER TABLE HOMEPAGE.PERSON ADD PERSON_ID nvarchar(36) NOT NULL DEFAULT '';
GO

-- adding DISPLAYNAME column
ALTER TABLE HOMEPAGE.PERSON ADD DISPLAYNAME nvarchar(256) NOT NULL DEFAULT '';
GO

-- adding SHOW_DISABLED_WIDGETS column
ALTER TABLE HOMEPAGE.PERSON ADD SHOW_DISABLED_WIDGETS SMALLINT DEFAULT 0 NOT NULL;
GO

-- USER_MAIL can now be even NULL
ALTER TABLE HOMEPAGE.PERSON ALTER COLUMN USER_MAIL nvarchar(256);
GO

-- copy USER_ID in PERSON_ID
UPDATE HOMEPAGE.PERSON  	
SET HOMEPAGE.PERSON.PERSON_ID = 	( SELECT PERSON_A.USER_ID
FROM HOMEPAGE.PERSON AS PERSON_A
WHERE PERSON_A.USER_ID = HOMEPAGE.PERSON.USER_ID)
WHERE EXISTS
  ( SELECT PERSON_B.USER_ID
    FROM HOMEPAGE.PERSON AS PERSON_B
    WHERE PERSON_B.USER_ID = HOMEPAGE.PERSON.USER_ID);
GO

-- copy USER_MAIL in DISPLAYNAME
UPDATE HOMEPAGE.PERSON  	
SET HOMEPAGE.PERSON.DISPLAYNAME = 	( SELECT PERSON_A.USER_MAIL
FROM HOMEPAGE.PERSON AS PERSON_A
WHERE PERSON_A.USER_ID = HOMEPAGE.PERSON.USER_ID)
WHERE EXISTS
  ( SELECT PERSON_B.USER_MAIL
    FROM HOMEPAGE.PERSON AS PERSON_B
    WHERE PERSON_B.USER_ID = HOMEPAGE.PERSON.USER_ID);
GO

-- copy SHOW_DISABLED_WIDGETS 
UPDATE HOMEPAGE.PERSON  	
SET HOMEPAGE.PERSON.SHOW_DISABLED_WIDGETS = ( 	SELECT DISTINCT (HOMEPAGE.USER_WIDGET_PREF.SHOW_DISABLED_WIDGETS)
												FROM HOMEPAGE.USER_WIDGET_PREF
												WHERE 	HOMEPAGE.USER_WIDGET_PREF.USER_ID = HOMEPAGE.PERSON.USER_ID AND 
														HOMEPAGE.USER_WIDGET_PREF.SHOW_DISABLED_WIDGETS <> 0)
WHERE EXISTS
  ( SELECT DISTINCT (HOMEPAGE.USER_WIDGET_PREF.USER_ID)
    FROM HOMEPAGE.USER_WIDGET_PREF
    WHERE HOMEPAGE.USER_WIDGET_PREF.USER_ID = HOMEPAGE.PERSON.USER_ID AND 
    HOMEPAGE.USER_WIDGET_PREF.SHOW_DISABLED_WIDGETS <> 0);
GO

-- renaming USER_ID to PERSON_ID, remove the old USER_ID which now is called PERSON_ID
-- remove the primary key on USER_ID and set it on PERSON_ID
-- Note: the drop for FK_USER_ID could fail
ALTER TABLE HOMEPAGE.PERSON DROP CONSTRAINT PK_USER_ID;
GO
ALTER TABLE HOMEPAGE.PERSON
   ADD CONSTRAINT PK_PERSON_ID PRIMARY KEY (PERSON_ID);
GO
ALTER TABLE HOMEPAGE.PERSON DROP COLUMN USER_ID;
GO

-----------------------------------------------------------------------------------------------------
-- WORKING ON HOMEPAGE.LOGINNAME
-----------------------------------------------------------------------------------------------------
-- Adding the new table LOGINAME
CREATE TABLE HOMEPAGE.LOGINNAME (
	PERSON_ID nvarchar(36) NOT NULL,
	LOGINNAME nvarchar(256) NOT NULL,
) ON [PRIMARY]
GO

-- Populate the new table LOGINAME
INSERT INTO HOMEPAGE.LOGINNAME (PERSON_ID, LOGINNAME) SELECT PERSON_ID, USER_MAIL FROM HOMEPAGE.PERSON;
GO

------------------------------------------------------------------------------------------------------
-- WORKING ON HOMEPAGE.USER_WIDGET_PREF
------------------------------------------------------------------------------------------------------
-- change USER_ID VARCHAR(256) to USER_ID VARCHAR(36), need to use a temp column TEMP_USER_ID
ALTER TABLE HOMEPAGE.USER_WIDGET_PREF ADD TEMP_USER_ID nvarchar(36);
GO

UPDATE HOMEPAGE.USER_WIDGET_PREF  	
SET HOMEPAGE.USER_WIDGET_PREF.TEMP_USER_ID = 	( SELECT USER_WIDGET_PREF_A.USER_ID
FROM HOMEPAGE.USER_WIDGET_PREF AS USER_WIDGET_PREF_A
WHERE USER_WIDGET_PREF_A.USER_WIDGET_PREF_ID = HOMEPAGE.USER_WIDGET_PREF.USER_WIDGET_PREF_ID)
WHERE EXISTS
  ( SELECT USER_WIDGET_PREF_B.USER_WIDGET_PREF_ID
    FROM HOMEPAGE.USER_WIDGET_PREF AS USER_WIDGET_PREF_B
    WHERE USER_WIDGET_PREF_B.USER_WIDGET_PREF_ID = HOMEPAGE.USER_WIDGET_PREF.USER_WIDGET_PREF_ID);
GO

ALTER TABLE HOMEPAGE.USER_WIDGET_PREF DROP COLUMN USER_ID;
GO

ALTER TABLE HOMEPAGE.USER_WIDGET_PREF ADD USER_ID nvarchar(36) NOT NULL DEFAULT '';
GO

UPDATE HOMEPAGE.USER_WIDGET_PREF  	
SET HOMEPAGE.USER_WIDGET_PREF.USER_ID = 	( SELECT USER_WIDGET_PREF_A.TEMP_USER_ID
FROM HOMEPAGE.USER_WIDGET_PREF AS USER_WIDGET_PREF_A
WHERE USER_WIDGET_PREF_A.USER_WIDGET_PREF_ID = HOMEPAGE.USER_WIDGET_PREF.USER_WIDGET_PREF_ID)
WHERE EXISTS
  ( SELECT USER_WIDGET_PREF_B.TEMP_USER_ID
    FROM HOMEPAGE.USER_WIDGET_PREF AS USER_WIDGET_PREF_B
    WHERE USER_WIDGET_PREF_B.USER_WIDGET_PREF_ID = HOMEPAGE.USER_WIDGET_PREF.USER_WIDGET_PREF_ID);

-- remove TEMP_USER_ID
ALTER TABLE HOMEPAGE.USER_WIDGET_PREF DROP COLUMN TEMP_USER_ID;
GO

-- remove SHOW_DISABLED_WIDGETS
DECLARE @CONST_NAME as nvarchar(128), @DROP_STMT as nvarchar(2000)
SET @CONST_NAME = (select name from sys.default_constraints where parent_object_id=OBJECT_ID('HOMEPAGE.USER_WIDGET_PREF') and parent_column_id=9)
SET @DROP_STMT = N'ALTER TABLE HOMEPAGE.USER_WIDGET_PREF DROP CONSTRAINT ' + @CONST_NAME
EXEC (@DROP_STMT)
GO
ALTER TABLE HOMEPAGE.USER_WIDGET_PREF DROP COLUMN SHOW_DISABLED_WIDGETS;
GO

-- UPDATE THE WIDGET_ICON using HOMEPAGE_CONTEXT_ROOT in HOMEPAGE.WIDGET
UPDATE HOMEPAGE.WIDGET 
	SET HOMEPAGE.WIDGET.WIDGET_ICON = '${HOMEPAGE_CONTEXT_ROOT}' + '' + SUBSTRING(HOMEPAGE.WIDGET.WIDGET_ICON,10,LEN(HOMEPAGE.WIDGET.WIDGET_ICON))
	WHERE HOMEPAGE.WIDGET.WIDGET_ICON like '/homepage/%';
GO

------------------------------------------------------------------------------------------------------
-- WORKING ON HOMEPAGE.HOMEPAGE_SCHEMA
------------------------------------------------------------------------------------------------------

UPDATE HOMEPAGE.HOMEPAGE_SCHEMA SET DBSCHEMAVER =  6;

-------------------------- END -----------------------------------------------------------------------

--------------------------------------
-- CREATE THE CONSTRAINTS
--------------------------------------
ALTER TABLE HOMEPAGE.LOGINNAME
	ADD CONSTRAINT PK_LOGINNAME_ID PRIMARY KEY (PERSON_ID,LOGINNAME)
GO

ALTER TABLE HOMEPAGE.LOGINNAME
	ADD CONSTRAINT FK_PERSON_ID FOREIGN KEY (PERSON_ID)
	REFERENCES HOMEPAGE.PERSON (PERSON_ID)
GO
	
ALTER TABLE HOMEPAGE.USER_WIDGET_PREF
    ADD CONSTRAINT FK_USER_ID FOREIGN KEY (USER_ID)
	REFERENCES HOMEPAGE.PERSON (PERSON_ID)
GO

ALTER TABLE HOMEPAGE.USER_WIDGET_PREF
    ADD CONSTRAINT FK_WIDGET_ID FOREIGN KEY (WIDGET_ID)
	REFERENCES HOMEPAGE.WIDGET (WIDGET_ID)
GO

ALTER TABLE HOMEPAGE.PREREQ 
	ADD CONSTRAINT FK_PREREQ_WIDGET FOREIGN KEY (WIDGET_ID)
	REFERENCES HOMEPAGE.WIDGET (WIDGET_ID)
	ON DELETE CASCADE
GO

ALTER TABLE HOMEPAGE.LOGINNAME
	ADD CONSTRAINT LOGINNAME_UNIQUE UNIQUE (LOGINNAME)
GO

--------------------------------------
-- CREATE THE INDEXES
--------------------------------------

CREATE UNIQUE INDEX USERID_WIDGET_UNIQUE
	ON HOMEPAGE.USER_WIDGET_PREF (USER_ID ASC, WIDGET_ID ASC)
GO

CREATE UNIQUE INDEX PERSON_EXID
	ON HOMEPAGE.PERSON (EXID)
GO

CREATE INDEX PERSON_USER_MAIL
	ON HOMEPAGE.PERSON (USER_MAIL)
GO

--------------------------------------
-- GRANT TO HOMEPAGE USER
--------------------------------------
GRANT DELETE,INSERT,SELECT,UPDATE ON HOMEPAGE.HOMEPAGE_SCHEMA TO HOMEPAGEUSER
GO
GRANT DELETE,INSERT,SELECT,UPDATE ON HOMEPAGE.PERSON TO HOMEPAGEUSER
GO
GRANT DELETE,INSERT,SELECT,UPDATE ON HOMEPAGE.LOGINNAME TO HOMEPAGEUSER
GO
GRANT DELETE,INSERT,SELECT,UPDATE ON HOMEPAGE.USER_WIDGET_PREF TO HOMEPAGEUSER
GO
GRANT DELETE,INSERT,SELECT,UPDATE ON HOMEPAGE.WIDGET TO HOMEPAGEUSER
GO
GRANT DELETE,INSERT,SELECT,UPDATE ON HOMEPAGE.PREREQ TO HOMEPAGEUSER
GO

COMMIT;
